load("@aspect_bazel_lib//lib:copy_file.bzl", "copy_file")
load("@aspect_bazel_lib//lib:copy_to_bin.bzl", "copy_to_bin")
load("@aspect_rules_js//js:defs.bzl", "js_binary", "js_test")
load("@aspect_rules_ts//ts:defs.bzl", "ts_project")
load("@aspect_rules_webpack//webpack:defs.bzl", "webpack_bundle", "webpack_devserver")
load("@gzgz_rules_sass//sass:defs.bzl", "sass_binary")
load("@npm//:defs.bzl", "npm_link_all_packages")

npm_link_all_packages(name = "node_modules")

package(default_visibility = ["//:__subpackages__"])

ts_project(
    name = "tsc",
    srcs = glob(
        [
            "src/**/*.ts",
            "src/**/*.tsx",
        ],
        exclude = [
            "src/**/*.spec.ts",
        ],
    ),
    allow_js = True,
    declaration = True,
    declaration_map = True,
    incremental = True,
    out_dir = "dist",
    resolve_json_module = True,
    root_dir = "src",
    source_map = True,
    tsconfig = "tsconfig.json",
    deps = [
        ":node_modules/@types/react",
        ":node_modules/@types/react-dom",
        ":node_modules/react",
        ":node_modules/react-dom",
    ],
)

# # React-js need document DOM API context to be executing which is available
# # when running the js app in a browser, so js_binary can NOT be used now since
# # it executes js app in NodeJS that doesn't have it.
# js_binary(
#     name="tsc_app",
#     data=[
#         ":tsc",
#         "package.json",
#     ],
#     chdir= package_name(),
#     entry_point = "dist/index.js",
# )

webpack_bundle(
    name = "webpack_build",
    srcs = [":tsc"],
    chdir = package_name(),
    entry_point = "dist/index.js",
    node_modules = ":node_modules",
    output_dir = False,
    tags = ["webpack"],
    webpack_config = "webpack.config.cjs",
    deps = [
        ":node_modules/webpack",
    ],
)

# # React-js need document DOM API context to be executing which is available
# # when running the js app in a browser, so js_binary can NOT be used now since
# # it executes js app in NodeJS that doesn't have it.
# js_binary(
#     name="webpack_app",
#     data=[
#         ":webpack_build",
#     ],
#     chdir= package_name(),
#     entry_point = "webpack_build.js",
# )

# Rule works!
# @TODO: find a better way!
# Note: We need to figure out first how to inject css into React app:
# Dynamically (inject during bundling) or statically (by linking statically generated file)
sass_binary(
    name = "styles",
    src = "src/styles.scss",
    output_dir = "dist",
    output_name = "styles.css",
)

# Rule works!
# @TODO: find a better way!
copy_file(
    name = "index_html",
    src = "src/index.html",
    out = "dist/index.html",
)

webpack_devserver(
    name = "serve",
    chdir = package_name(),
    data = [
        ":index_html",
        ":node_modules/webpack-cli",
        ":node_modules/webpack-dev-server",
        ":styles",
        ":tsc",
        ":webpack_build",
    ],
    entry_points = {
        "dist/webpack_build.js": ":webpack_build",
    },
    grant_sandbox_write_permissions = True,
    log_level = "debug",
    node_modules = ":node_modules",
    tags = ["webpack_serve"],
    webpack_config = "webpack.config.cjs",
)

# Tests infrastructure
# Note! Please make sure to add exclude = ["src/**/*.spec.ts"] to :tsc target
# @TODO find a way to split tests from the source code (`:tsc` and `:tsc_tests`)
ts_project(
    name = "tsc_tests",
    testonly = True,
    srcs = glob([
        "src/**/*.spec.ts",
    ]),
    allow_js = True,
    declaration = True,
    declaration_map = True,
    incremental = True,
    out_dir = "dist",
    resolve_json_module = True,
    root_dir = [
        "src",
        "dist",
    ],
    source_map = True,
    tsconfig = {
        "compilerOptions": {
            "strict": True,
            "target": "ESNext",
            "module": "ESNext",
            "incremental": True,
            "sourceMap": True,
            "inlineSources": True,
            "noUnusedLocals": True,
            "moduleResolution": "node",
            "esModuleInterop": True,
            "importHelpers": True,
            "allowSyntheticDefaultImports": True,
            "forceConsistentCasingInFileNames": True,
            "declaration": True,
            "declarationMap": True,
            "useUnknownInCatchVariables": False,
            "isolatedModules": True,
            "noImplicitOverride": True,
            "preserveConstEnums": True,
            "skipLibCheck": True,
            "allowJs": True,
            "emitDeclarationOnly": False,
            "resolveJsonModule": True,
            "strictNullChecks": True,
            "jsx": "react-jsx",
            "outDir": "dist",
            "rootDir": "src",
            # This is super to 1. use "rootDirs" and have two source paths there:
            # - the "src" path for spec files (`*.spec.ts`) only declared in srcs of the `tsc_tests` rule
            # - the "dist" path for all compiled TS files into the /dist folder.
            # Background. When building the tsc_tests rule it has only "src/**/*.spec.ts" specified in the srcs arg
            # but no actual ts files. Most likely every `spec.ts` file will have an import like `./util1.js` so that
            # `spec.ts` will also need a source file (which we can't provide in the tsc_test because it will raise the
            # conflict between :tsc and :tsc_tests when both rules uses the same source files which is forbidden).
            # These missing *.ts source files are exist after building the `:tsc` rule which will store the output in
            # the /dist folder (as declared in :tsc). So all we need for :tsc_tests and `spec.ts` files to properly reference
            # to that /dist folder. For this we need this:
            # - add the `":tsc",` as a dependency for `:tsc_tests`
            # - tell the Typescript where to find compiled ts sources (they are in /dist). Typescript allows to use "rootDirs"
            # property in tsconfig.json (do not mix and much with another "rootDir" prop), so we can tell the typescript where
            # to look for .ts source files for building the target
            "rootDirs": [
                "src",
                "dist",
            ],
        },
        "exclude": ["node_modules"],
    },
    validate = False,
    deps = [
        ":node_modules/@types/chai",
        ":node_modules/@types/mocha",
        ":node_modules/chai",
        ":node_modules/mocha",
        ":tsc",
    ],
)

copy_to_bin(
    name = "package_json",
    srcs = ["package.json"],
)

js_test(
    name = "test",
    size = "small",
    timeout = "short",
    args = ["--no-build"],
    data = [
        ".mocharc.json",
        ":package_json",
        ":tsc_tests",
        "//tools/test-mocha:mocha_test_lib",
    ],
    entry_point = "//tools/test-mocha:mocha_test_lib",
    env = {},
    log_level = "error",
    tags = ["mocha-test"],
)

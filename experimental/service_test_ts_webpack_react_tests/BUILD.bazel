load("@aspect_bazel_lib//lib:copy_file.bzl", "copy_file")
load("@aspect_bazel_lib//lib:copy_to_bin.bzl", "copy_to_bin")
load("@aspect_rules_js//js:defs.bzl", "js_binary", "js_test")
load("@aspect_rules_ts//ts:defs.bzl", "ts_project")
load("@gzgz_rules_sass//sass:defs.bzl", "sass_binary")
load("@npm//:defs.bzl", "npm_link_all_packages")
load("//tools/webpack:webpack.bzl", "webpack")

npm_link_all_packages(name = "node_modules")

package(default_visibility = ["//:__subpackages__"])

ts_project(
    name = "tsc",
    srcs = glob(
        [
            "src/**/*.ts",
            "src/**/*.tsx",
        ],
        exclude = [
            "src/**/*.spec.ts",
        ],
    ),
    allow_js = False,
    declaration = True,
    declaration_map = True,
    incremental = True,
    out_dir = "dist",
    resolve_json_module = True,
    root_dir = "src",
    source_map = True,
    tsconfig = "tsconfig.json",
    deps = [
        ":node_modules/@types/react",
        ":node_modules/@types/react-dom",
        ":node_modules/react",
        ":node_modules/react-dom",
    ],
)

# @TODO: find a better way!
sass_binary(
    name = "styles",
    src = "src/styles.scss",
    output_dir = "dist",
    output_name = "styles.css",
)

# @TODO: find a better way!
copy_file(
    name = "index_html",
    src = "src/index.html",
    out = "dist/index.html",
)

# Macro adds:
# :webpack_build
# :webpack_build.serve
webpack(
    name = "webpack_build",
    srcs = [":tsc"],
    data = [
        ":index_html",
        ":styles",
    ],
    entry_point = "dist/index.js",
    include_webpack_serve = True,
    webpack_config = "webpack.config.cjs",
)

# Tests infrastructure
# Note! Please make sure to add exclude = ["src/**/*.spec.ts"] to :tsc target
# @TODO find a way to split tests from the source code (`:tsc` and `:tsc_tests`)
ts_project(
    name = "tsc_tests",
    testonly = True,
    srcs = glob([
        "src/**/*.spec.ts",
    ]),
    allow_js = False,
    declaration = True,
    declaration_map = True,
    incremental = True,
    out_dir = "dist",
    resolve_json_module = True,
    root_dir = [
        "src",
        "dist",
    ],
    source_map = True,
    tsconfig = {
        "compilerOptions": {
            "strict": True,
            "target": "ESNext",
            "module": "ESNext",
            "incremental": True,
            "sourceMap": True,
            "inlineSources": True,
            "noUnusedLocals": True,
            "moduleResolution": "node",
            "esModuleInterop": True,
            "importHelpers": True,
            "allowSyntheticDefaultImports": True,
            "forceConsistentCasingInFileNames": True,
            "declaration": True,
            "declarationMap": True,
            "useUnknownInCatchVariables": False,
            "isolatedModules": True,
            "noImplicitOverride": True,
            "preserveConstEnums": True,
            "skipLibCheck": True,
            # "allowJs": True,
            "isolatedDeclarations": True,
            "emitDeclarationOnly": False,
            "resolveJsonModule": True,
            "strictNullChecks": True,
            "jsx": "react-jsx",
            "outDir": "dist",
            "rootDir": "src",
            # This is super to 1. use "rootDirs" and have two source paths there:
            # - the "src" path for spec files (`*.spec.ts`) only declared in srcs of the `tsc_tests` rule
            # - the "dist" path for all compiled TS files into the /dist folder.
            # Background. When building the tsc_tests rule it has only "src/**/*.spec.ts" specified in the srcs arg
            # but no actual ts files. Most likely every `spec.ts` file will have an import like `./util1.js` so that
            # `spec.ts` will also need a source file (which we can't provide in the tsc_test because it will raise the
            # conflict between :tsc and :tsc_tests when both rules uses the same source files which is forbidden).
            # These missing *.ts source files are exist after building the `:tsc` rule which will store the output in
            # the /dist folder (as declared in :tsc). So all we need for :tsc_tests and `spec.ts` files to properly reference
            # to that /dist folder. For this we need this:
            # - add the `":tsc",` as a dependency for `:tsc_tests`
            # - tell the Typescript where to find compiled ts sources (they are in /dist). Typescript allows to use "rootDirs"
            # property in tsconfig.json (do not mix and much with another "rootDir" prop), so we can tell the typescript where
            # to look for .ts source files for building the target
            "rootDirs": [
                "src",
                "dist",
            ],
        },
        "exclude": ["node_modules"],
    },
    validate = False,
    deps = [
        ":node_modules/@types/chai",
        ":node_modules/@types/mocha",
        ":node_modules/chai",
        ":node_modules/mocha",
        ":tsc",
    ],
)

copy_to_bin(
    name = "package_json",
    srcs = ["package.json"],
)

js_test(
    name = "test",
    size = "small",
    timeout = "short",
    args = ["--no-build"],
    data = [
        ".mocharc.json",
        ":package_json",
        ":tsc_tests",
        "//tools/test-mocha:mocha_test_lib",
    ],
    entry_point = "//tools/test-mocha:mocha_test_lib",
    env = {},
    log_level = "error",
    tags = ["mocha-test"],
)
